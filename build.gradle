import com.github.benmanes.gradle.versions.VersionsPlugin

buildscript {
    apply from: "$rootDir/buildsystem/dependencies.gradle"

    repositories {
        mavenCentral()
        google()
        gradlePluginPortal()
    }

    dependencies {
        classpath libraries.android_gradle
        classpath libraries.kotlin
        classpath libraries.maven
        classpath libraries.dokka
        classpath libraries.oss
        classpath libraries.detekt
        classpath libraries.gradle_versions
    }
}

apply plugin: 'io.gitlab.arturbosch.detekt'
detekt {
    toolVersion = "1.17.1"
    input = files(subprojects.collect{
        return [new File(it.projectDir, '/src/main/kotlin')]
    })
    buildUponDefaultConfig = true
    config = files("$projectDir/detekt-config.yml")
    baseline = file("$projectDir/detekt-baseline.xml")

    reports {
        html.enabled = true
        xml.enabled = true
    }
}


allprojects {
    repositories {
        jcenter()
    }
    apply plugin: VersionsPlugin

    def isNonStable = { String version ->
        def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any {  version.toUpperCase().contains(it) }
        def regex = /^[0-9,.v-]+(-r)?$/
        return !stableKeyword && !(version ==~ regex)
    }

    tasks.named("dependencyUpdates").configure {
        rejectVersionIf {isNonStable(it.candidate.version)}
    }
}
